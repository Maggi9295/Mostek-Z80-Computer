10 REM N I M M S P I E L   NOV.78 BY F. KREUTZBURG, ERWEITERT APR.81/WEFERS
20 REM
30 REM
40 REM H = ANZAHL DER HOELZER
50 REM M = ANZAHL DER ZU NEHMENDEN HOELZER
60 REM S = SPIELERHOELZER
70 REM R = RECHNERHOELZER
80 PRINT        "                              N I M M S P I E L"
90 PRINT
100 PRINT
110 PRINT" Ich beglueckwuensche Sie zu Ihrem Mut, gegen mich antreten zu wollen!"
120 PRINT : VL=0 : GW=0
130 H =INT(RND(1)*10)+13
140 M = INT (RND(1)*3)+3
150 IF D$ = "J" THEN 380
160 PRINT
170 PRINT "Moechten Sie die Spielregeln? (J/N):";
180 PRINT
190 INPUT A$ : IF A$<>"J" AND A$<>"N" THEN 190
200 IF A$ = "N" THEN 400
210 PRINT
220 PRINT "Die Spielregeln:"
230 PRINT "----------------"
240 PRINT
250 PRINT"Von insgesamt";H;"Streichhoelzern muss jeder mindestens 1, "
260 PRINT"maximal jedoch" ;M;"Hoelzer wegnehmen."
270 PRINT"Derjenige, der das letzte Hoelzchen zu nehmen hat, ist der Verlierer."
280 PRINT
290 PRINT "Sind Sie Raucher? (J/N):";
300 INPUT A$
310 IF A$<>"J" AND A$<>"N" THEN 290 ELSE IF A$="J" THEN 360
320 PRINT
330 PRINT "Ich auch nicht. Dann brauchen wir ja keinen Aschenbecher"
340 PRINT"und es kann sofort losgehen!"
350 GOTO 380
360 PRINT"L U F T V E R P E S T E R !    Aber trotzdem: Sollten Sie jetzt"
370 PRINT"auch noch aufgeregt sein, stecken Sie sich erst 'mal eine an!"
371 PRINT"Ich kann warten!
380 PRINT
390 PRINT
400 PRINT"H  A  L  T  !"
410 PRINT
420 PRINT"Noch eins! Falls sich sich herausstellen sollte, dass Sie verlieren,"
430 PRINT "schalten Sie mir bitte nicht den Saft ab!!"
440 PRINT
450 PRINT "Nun aber. Sie duerfen anfangen."
460 PRINT "Bitte nehmen Sie nur bis zu";M;"Streichhoelzer!"
470 PRINT 
480 PRINT TAB(30);H; "Streichhoelzer
481 KC=0 : IF H-INT((H-1)/(M+1))*(M+1)-1=0 THEN KC=1
490 PRINT
500 PRINT "Bitte nehmen Sie:";
510 INPUT S
520 IF S < 1 THEN 550
530 IF S>M THEN 550
540 GOTO 570
550 PRINT "Haben Sie die Spielregeln nicht verstanden?"
560 GOTO 500
570 H = H -S
580 PRINT
590 PRINT "Bleiben"; TAB(30);H;"Streichhoelzer"
600 PRINT
610 IF H = 1 THEN 815
620 PRINT "Bitte RETURN-Taste druecken!!!"
630 INPUT TTT
640 R = H - INT ((H-1)/(M+1))*(M+1)
650 R = R - 1
660 IF R <> 0 THEN 690
680 R = 1 + INT (RND(1)*(M-1))
690 H = H - R
700 PRINT "Ich nehme:";R;TAB(30);H;"Streichhoelzer"
710 IF H > 1 THEN 490
720 IF KC=0 THEN GW=GW+1
730 PRINT 
740 PRINT
741 IF KC   THEN 750
745 IF GW>1 THEN 970
750 PRINT " Damit duerften Sie verloren haben!"
760 PRINT
770 PRINT
780 PRINT "MOECHTEN SIE NOCH MAL SPIELEN? (J/N): ";
790 INPUT D$
800 IF D$ ="J" THEN 130
810 END
815 VL=VL+1 : IF VL=2 THEN 850
816 IF VL>2 THEN 900
820 PRINT "Nun ja, Sie haben zwar gewonnen, aber bestimmt nur deshalb,"
830 PRINT "weil Sie mich betrogen haben."
840 GOTO 760
850 PRINT:PRINT
860 PRINT"Sie haben ja schon wieder gewonnen! Ich glaube fast,"
870 PRINT" Sie haben mich durchschaut!"
880 GOTO 760
900 PRINT:PRINT
910 PRINT"Ich gebe auf !!"
920 PRINT
930 PRINT"Das macht ja keinen Spass mehr, wenn man so oft verliert!"
940 PRINT
950 PRINT"Bitte S P I E L E R W E C H S E L !!"
960 PRINT:PRINT: RUN
970 IF GW>2 THEN 1010
980 PRINT"Sie haben schon wieder verloren, obwohl Sie haetten gewinnen koennen!"
990 PRINT"Strengen Sie sich 'mal etwas mehr an!!"
1000 GOTO 760
1010 IF GW>3 THEN 1050
1020 PRINT"Vielleicht sollten Sie es lieber 'mal mit 'Mensch aergere Dich nicht'"
1030 PRINT"versuchen! Darin sind Sie bestimmt besser!  Aber trotzdem:"
1040 GOTO 760
1050 PRINT"Sie sind leider ein hoffnungsloser Fall!!!"
1060 PRINT:PRINT
1070 GOTO 950