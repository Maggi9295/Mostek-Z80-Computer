10 Z$="GO"
20 B1=1
30 M=17.95:F1=5.25:N=7.5:R0=926:V0=1.29:T=0:H0=60:R=R0+H0:A=-3.425
40 R1=0:A1=8.84361E-04:R3=0:A3=0:M1=7.45:M0=M1:B=750:T1=0:F=0:P=0:N=1
50 M2=0:S=0:C=0:L0=0
60 IF LEFT$(Z$,1)="Y" THEN 1240
70 PRINT:PRINT"MONDLANDUNG SIMULATION":PRINT
80 PRINT"KENNEN SIE SCHON DIE REGELN?";
90 INPUT Q$
100 IFLEFT$(Q$,1)="J" THEN 130
110 IF LEFT$(Q$,1)="N" THEN 140
120 PRINT "BITTE NUR JA ODER NEIN ANTWORTEN!":GOTO 90
130 REM LEAVE OUT CHOICE OF NAUTICAL OR METRIC SYSTEM
140 Z=1852.8:M$="METER" :G3=3.6:N$="KILOMETER" :G5=1000
150 IF B1=3 THEN 670
160 IF LEFT$(Q$,1)="J" THEN 420
170 PRINT
180 PRINT "SIE SIND DER KAPITAEN EINES RAUMSCHIFFES"
190 PRINT "SIE MUESSEN JETZT IHREN STUETZPUNKT AUF DEM MOND"
200 PRINT "ANFLIEGEN UND DORT LANDEN."
210 PRINT "DIE NOTWEDIGE STEUERDATEN GEBEN SIE DEM COMPUTER"
220 PRINT "UEBER DIE TASTATUR.(ZEIT,SCHUB,SCHUBWINKEL)"
230 PRINT
240 PRINT "DIE SCHUBRICHTUNG WIRD SO DEFINIERT:"
250 PRINT
260 PRINT "+,-180 GRAD ERGIBT EINE BESCHL. RICHTUNG MOND"
270 PRINT "+90 GRAD IST IN FLUGRICHTUNG,PARALLEL ZUR OBERFLAECHE"
280 PRINT "-90 GRAD IST GEGEN FLUGRICHTUNG"
290 PRINT "0 GRAD BESCHLEUNIGT VON DER OBERFLAECHE WEG"
300 PRINT
310 PRINT TAB(30);"-180/+180":PRINT TAB(34);"^"
320 PRINT TAB(27);"-90  <-+->  +90":PRINT TAB(34);"!":PRINT TAB(34);"0"
330 PRINT TAB(23);"<<< RICHTUNG DER UMLAUFBAHN <<<":PRINT
340 PRINT TAB(27);"MONDOBERFLAECHE":PRINT:PRINT
350 PRINT "WINKELANGABEN ZWISCHEN -180 UND +180 GRAD WERDEN ANGENOMMEN"
360 PRINT TAB(15);"DRUECKEN SIE DIE CARRIAGE RETURN TASTE";:INPUT K$
370 PRINT "1 TREIBSTOFF-EINHEIT=1 SEK BEI MAXIMUM SCHUB"
380 PRINT "SCHUBKRAFT KANN 0(NULL) SEIN,ODER EIN WERT"
390 PRINT "ZWISCHEN 10% UND 100%":PRINT
400 PRINT "MINUS WERTE FUER SCHUB ODER ZEIT SIND NICHT ERLAUBT!"
410 PRINT
420 PRINT "BENOETIGTE EINGABEN: ZEIT IN SEK.......(T)"
430 PRINT "                     SCHUB IN % .......(P)"
440 PRINT "                   SCHUBRICHTUNG......(GRAD)"
450 PRINT
460 IF LEFT$(Q$,1)= "J" THEN 490
470 PRINT "ZUM BEISPIEL---T,P,A? 10,65,-32"
480 PRINT "UM ABZUBRECHEN: 0,0,0 EINGEBEN!"
490 PRINT "DATEN AUSGEGEBEN: GESAMTZEIT IN SEK."
500 PRINT "                  HOEHE IN METER"
510 PRINT "                  ENTFERNUNG VOM LANDEPLATZ"
520 PRINT "                  VERTIKALGESCHWINDIGKEIT IN M/SEK"
530 PRINT "                  HORIZONTALGESCHWINDIGKEIT IN M/SEK"
540 PRINT "                  NOCH VERBLEIBENDE TREIBSTOFFEINHEITEN"
550 PRINT:PRINT TAB(15);"TASTE CARRIAGE RETURN DRUECKEN !";:INPUT K$
560 GOTO 670
570 PRINT
580 INPUT T1,F,P:F=F/100:IF T1<0 THEN 940
590 IF T1=0 THEN 1170
600 IF F<0 THEN 970
610 IF ABS(F-.05)>1 THEN 970
620 IF ABS(F-.05)<.05 THEN 970
630 IF ABS(P)>180 THEN 950
640 N=T1/20
650 T1=T1/N:P=P*3.14159/180:S=SIN(P):C=COS(P):M2=M0*T1*F/B
660 R3=5*R0*((V0/R)^2)+R*A1*A1:A3=-2*R1*A1/R
670 FOR I=1TON
680 IF M1=0 THEN 710
690 M1=M1-M2:IF M1>0 THEN 720
700 F=F*(1+M1/M2):M2=M1+M2:PRINT "KEIN TREIBSTOFF MEHR":M1=0:GOTO 720
710 F=0:M2=0
720 M=M-.5*M2:R4=R3:R3=.5*R0*((V0/R)^2)+R*A1*A1
730 R2=(3*R3-R4)/2+5.26E-03*F1*F*C/M:A4=A3:A3=-2*R1*A1/R
740 A2=(3*A3-A4)/2+5.26E-03*F1*F*S/(M*R)
750 X=R1*T1+.5*R2*T1*T1
760 R=R+X:H0=H0+X:R1=R1+R2*T1:A=A+A1*T1+.5*A2*T1*T1
770 A1=A1+A2*T1:M=M-.5*M2:T=T+T1
780 IF H0<3.28783E-04 THEN 800
790 NEXT I
800 H=H0*Z:H1=R1*Z:D=R0*A*Z:D1=R*A1*Z:T2=M1*B/M0
810 IF L0/10-INT(L0/10)=0 THEN GOSUB 1260
820 PRINT TAB(1);T;TAB(10);H;TAB(23);D;TAB(37);H1;TAB(49);D1;TAB(60);T2;
830 PRINT "  ";
840 L0=L0+1
850 IF H0<3.28783E-04 THEN 890
860 IF R0*A>164.474 THEN 1110
870 IF M1>0 THEN 580
880 T1=20:F=0:P=0:GOTO 630
890 IF R1<-8.21975E-04 THEN 1050
900 IF ABS(R*A1)>4.93174E-04 THEN 1050
910 IF H0<-3.28783E-04 THEN 1050
920 IF ABS(D)>10*Z THEN 1130
930 GOTO 1020
940 PRINT "NEGATIVE ZEIT UNERLAUBT!!":GOTO570
950 PRINT "BEI DIESEM DREH SPINNEN SIE WOHL!!!"
960 GOTO 570
970 PRINT:PRINT"SCHUBKRAFT UNMOEGLICH!!";:IF F<0 THEN 1010
980 IF F-.05<.05 THEN 1000
990 PRINT "ZU GROSS":GOTO 570
1000 PRINT "ZU KLEIN":GOTO 570
1010 PRINT "MINUS WERT":GOTO 570
1020 PRINT "MONDBASIS HIER-WIR GRATULIEREN"
1030 PRINT "SIE SIND OHNE SCHADEN GELANDET!"
1040 PRINT :GOTO 1180
1050 PRINT :PRINT "AUFPRALL!!!!!!!!!!"
1060 PRINT "SIE HABEN EINEN";ABS(H);"METER TIEFEN KRATER GESCHLAGEN!!!"
1070 X1=SQR(D1*D1+H1*H1)*G3
1080 PRINT "IHRE AUFPRALLGESCHWINDIGKEIT BETRUG";X1;"KM/ST"
1090 PRINT "IHRE UEBERRESTE WERDEN UNVERZUEGLICH DER OWAG ZUGESTELLT!"
1100 GOTO 1180
1110 PRINT:PRINT "SIE FLIEGEN JETZT IN DEN WELTRAUM HINAUS! GUTE REISE!!"
1120 GOTO 1180
1130 PRINT "SIE SIND SICHER GELANDET, ABER";ABS(D/G5);"KM VON DER BASIS ENTFERNT!"
1140 PRINT "ICH GRATULIERE IHNEN ZU DEM ERFOLG,"
1150 PRINT "UND DEM BEVORSTEHENDEN FUSSMARSCH ZUR BASIS."
1160 GOTO 1180
1170 PRINT "ANFLUG ABGEBROCHEN....."
1180 PRINT "MOECHTEN SIE NOCHMAL VERSUCHEN?":INPUT Z$
1190 IF LEFT$(Z$,1)="J" THEN 30
1200 IF LEFT$(Z$,1)="N" THEN 1220
1210 GOTO 1180
1220 PRINT "SCHADE,UND GERADE ALS SIE ENDLICH BESSER WERDEN"
1230 END
1240 PRINT:PRINT "GUT.----WIR STARTEN WIEDER"
1250 GOTO 670
1260 PRINT:PRINT TAB(1);"TIME";TAB(10);"HOEHE";TAB(23);"ENTFERNUNG";
1270 PRINT TAB(34);"VERT.GESCHW.HORIZ.GESCHW.TREIBSTOFF   T,P,A?"
1280 RETURN
1290 END