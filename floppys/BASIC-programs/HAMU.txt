10 WIDTH 80:PAG$=CHR$(126)+CHR$(18)+CHR$(126)+CHR$(28)
20 PRINT PAG$
30 PRINT:PRINT:PRINT
50 PRINT ,"   HH     HH      A      MM        MM  UU     UU"
51 PRINT ,"   HH     HH    A   A    MMM      MMM  UU     UU"
52 PRINT ,"   HH     HH  AA     AA  MM M   M  MM  UU     UU"
53 PRINT ,"   HHHHHHHHH  AAAAAAAAA  MM  M M   MM  UU     UU"
54 PRINT ,"   HH     HH  AA     AA  MM   M    MM  UU     UU"
55 PRINT ,"   HH     HH  AA     AA  MM        MM  UU     UU"
56 PRINT ,"   HH     HH  AA     AA  MM        MM   UUUUUUU"
57 PRINT:PRINT
60 PRINT ,"    TRY YOUR LUCK AT GOVERNING ANCIENT SUMERIA"
65 PRINT:PRINT
70 PRINT "       THE OBJECT OF THE GAME IS TO TRY TO STAY IN OFFICE FOR ONE YEAR"
75 PRINT:PRINT
90 INPUT "              DO YOU WANT THE COMPLEXITY OF MAINTAINING AN ARMY";ANS$
91 PRINT:ARMY=0:SD=0:ZZ=0
92 IF ANS$="YES" OR ANS$="Y" THEN ARMY=1:GOSUB 100:GOTO 150
93 IF ANS$<>"N" AND ANS$<>"NO" THEN GOSUB 95:GOTO 90 ELSE GOSUB 100:GOTO 150
95 PRINT:PRINT ,"THE PROSPECTIVE RULER OF A NATION MUST BE ABLE"
96 PRINT ,"TO COMMUNICATE BETTER THAN YOU HAVE BEEN DOING!"
97 PRINT ,"PLEASE ... ENTER 'Y' FOR 'YES' OR 'N' FOR 'NO'."
98 UP$=CHR$(126)+CHR$(12)
99 PRINT UP$;UP$;UP$;UP$;UP$;:RETURN
100 PRINT PAG$
110 PRINT:PRINT ,"THE FACTS OF ANCIENT SUMERIAN LIFE"
120 PRINT:PRINT ,"(1) YOUR PEOPLE REQUIRE 20 BUSHELS OF GRAIN"
121 PRINT ,"    PER PERSON PER MONTH TO SURVIVE."
122 PRINT:PRINT ,"(2) ONE PERSON CAN HARVEST ONLY 10 ACRES PER MONTH.
123 PRINT:PRINT ,"(3) YOU NEED 1/2 BUSHEL OF GRAIN PER ACRE TO PLANT."
124 PRINT:PRINT ,"(4) YOU MAY BUY OR SELL LAND EACH MONTH AT THE"
125 PRINT ,"    PREVAILING MARKET PRICE."
126 TERM=13:IF ARMY=0 THEN 135
127 PRINT:PRINT ,"(5) TO MAINTAIN YOUR ARMY EACH MONTH, REQUIRES"
128 PRINT ,"    AN ADDITIONAL 5 BUSHELS OF GRAIN PER SOLDIER PER MONTH."
129 PRINT:PRINT ,"(6) YOU WILL BE WARNED WHEN ANY INVADERS ARE"
130 PRINT ,"    WITHIN 50 MILES OF THE CITY; BUT THESE ARE EXTREMELY"
131 PRINT ,"    DANGEROUS TIMES, SO BEWARE OF POSSIBLE SNEAK ATTACKS!"
135 PRINT:PRINT:PRINT ,CHR$(126);CHR$(24);
140 LINE INPUT "PRESS 'RETURN' KEY TO CONTINUE THE GAME ...";ANS$
141 IF ANS$="Q" OR ANS$="QU" OR ANS$="QUIT" THEN 9990
142 RETURN
150 D1=0:P1=0:D=0:I1=INT(10*RND(1))+25:I2=INT(10*RND(3))+25
160 M=0:POP=99+INT(16*RND(5)):H=2129+INT(600*RND(599))
170 E=253+INT(200*RND(199)):B=H-E:A=1000:Y=INT(H/A)
180 I=5:Q=10:S=0:BX=B
200 P=INT(10*RND(7)+16):GOSUB 201:GOTO 345
201 PRINT PAG$
205 FOR K=0 TO 79
210 PRINT "-";
211 NEXT K
220 PRINT:PRINT ,"HAMURABI, I BEG TO REPORT TO YOU ..."
225 PRINT:M=M+1
230 PRINT ,"THAT IN MONTH";M;"OF YOUR ILLUSTRIOUS REIGN,
231 PRINT ,:IF D=0 THEN PRINT "NO "; ELSE PRINT "ONLY";D;
235 PRINT "PEOPLE STARVED, WHILE";I;"NEW SUBJECTS CAME TO THE CITY"
240 IF ARMY<>0 THEN IF SD>0 THEN PRINT ,"AND";SD;"OF YOUR BRAVE SOLDIERS DIED"
245 POP=POP+I
250 PRINT ,"THE TOTAL POPULATION IS NOW";POP
260 PRINT ,"THE CITY PRESENTLY OWNS";A;"ACRES OF FARMLAND"
270 PRINT ,"YOU HARVESTED";Y;"BUSHELS PER ACRE";
280 PRINT " BUT RATS ATE";E;"BUSHELS"
290 PRINT ,"YOU NOW HAVE A TOTAL OF";B;"BUSHELS IN STORAGE"
300 IF M=TERM THEN 800
310 PRINT ,"THE PRICE OF LAND IS CURRENTLY";P;"BUSHELS PER ACRE"
315 IF ARMY=0 THEN 330
316 IF I2>25+INT(10*RND(11)) THEN 330
320 PRINT ,"THERE ARE";I1;"INVADERS";I2;"MILES FROM HERE"
330 PRINT
331 FOR K=0 TO 79
335 PRINT "-";
340 NEXT K
341 RETURN
345 PRINT
346 IF ARMY=0 THEN L=12 ELSE L=14
350 GOSUB 1000:PRINT ,"HOW MANY ADDITIONAL ACRES DO YOU WANT TO BUY";
351 Q=0:INPUT Q 
352 IF Q<0 THEN 350
353 IF P*Q<=B THEN 360
354 GOSUB 900:PRINT ,"HAMURABI, THINK AGAIN, YOU ONLY OWN";B;"BUSHELS!"
356 GOTO 350
360 IF Q=0 THEN 370
361 A=A+Q:B=B-P*Q:C=0
362 GOSUB 1000:PRINT ,"YOU NOW OWN";B;"BUSHELS AND";A;"ACRES OF FARMLAND"
363 GOTO 400
370 GOSUB 1000:PRINT ,"HOW MANY ACRES DO YOU WANT TO SELL";
371 Q=0:INPUT Q:IF Q<0 THEN 360
372 IF Q<A THEN 380
373 GOSUB 900:PRINT ,"TRY AGAIN, HAMURABI, YOU ONLY HAVE";A;"ACRES!"
375 GOTO 370
380 A=A-Q:B=B+P*Q:C=0
381 GOSUB 1000:PRINT ,"YOU NOW OWN";B;"BUSHELS AND";A;"ACRES OF FARMLAND"
400 GOSUB 1000:PRINT ,"HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE";
402 Q=0:INPUT Q:IF Q<=0 THEN 400
403 IF Q<=B THEN 410
404 GOSUB 900:PRINT ,"THINK AGAIN, YOU ONLY HAVE";B;"BUSHELS OF GRAIN!"
405 GOTO 400
410 B=B-Q:C=1
411 IF Q/20<POP THEN GOSUB 1000:PRINT ,"MURDERER!!! "; ELSE GOSUB 1000:PRINT ,
412 PRINT "YOU NOW OWN";B;"BUSHELS OF GRAIN"
420 GOSUB 1000:PRINT ,"HOW MANY ACRES DO YOU WANT TO PLANT";
421 D=0:INPUT D:IF D<0 THEN 420
422 IF D=0 THEN 500
423 IF D<=A THEN 430
424 GOSUB 900:PRINT ,"KEEP TRYING, YOU HAVE ONLY";A;"ACRES!"
425 GOTO 420
430 IF D/2<=B THEN 440
431 GOSUB 900:PRINT ,"BUT HAMURABI, YOU ONLY HAVE";B;"BUSHELS OF GRAIN!"
432 GOTO 420
440 IF D-1<10*POP THEN 450
441 GOSUB 900:PRINT "BUT YOU ONLY HAVE";POP;"PEOPLE TO TEND THE FIELDS!"
442 GOTO 420
450 B=B-INT(D/2):GOSUB 1000:PRINT ,"YOU NOW HAVE";B;"BUSHELS OF GRAIN LEFT"
500 IF ARMY=0 THEN 515
510 GOSUB 1000:PRINT ,"HOW MANY SOLDIERS ARE TO BE IN YOUR ARMY";
511 S=0:INPUT S:IF S<0 THEN 510
512 IF S>POP-D/10 THEN GOSUB 900:PRINT ,"BUT WITH";D/10;"PEOPLE TENDING THE FIELDS, YOU HAVE ONLY";INT(POP-D/10);"PEOPLE LEFT!":GOTO 510
513 IF S*5>B THEN GOSUB 900:PRINT ,"ONLY";B;"BUSHELS OF GRAIN LEFT, HAMU ...":GOTO 510
514 B=B-INT(S*5)
515 GOSUB 1010
518 PRINT ,"SUMERIAN NEWS REPORTS FOR MONTH";M;"OF YOUR REIGN ..."
519 PRINT
520 C=INT(20*RND(17)):Y=1+INT(6*RND(19))
521 IF C>=18 THEN PRINT ,"THE GODS HAVE BLESSED US WITH A RECORD HARVEST!":Y=7
522 H=INT(D*Y):E=0
523 IF Y<2 THEN PRINT ,"SPARSE RAINFALL HAS KILLED THE HARVEST THIS MONTH"
524 C=1+INT(6*RND(23))-INT(B/3000)-INT(B/6000)
525 IF C<1 THEN C=1
526 IF C>2 THEN 529 ELSE C=INT(4*RND(29)+3.5-INT(B/6000))
527 E=INT(B/C)
528 PRINT ,"THE RAT PROBLEM IS GETTING SERIOUS"
529 B=B-E+H
530 C=1+INT(5*RND(31)):BB=INT(5*RND(37))
531 XX=INT(7*RND(43))-4:IF XX<0 THEN XX=0
532 I=INT(C*(20*A+B)/POP/100+1+XX*30)
533 IF XX<>0 THEN PRINT ,"THERE HAS BEEN AN INFLUX OF REFUGEES!"
534 D=POP-INT(Q/20):IF D<0 THEN D=0
535 P1=INT(P1+D*100/POP/M):POP=POP-D:D1=D1+D
536 IF P1>INT(10*RND(47))+5 THEN 600
537 SD=0:IF ARMY=0 THEN 570
540 I2=I2-INT(25*RND(59))+10:IF I2<0 THEN I2=0
541 IF I2>INT(5*RND(67)+7) THEN 570
542 PRINT ,"THE CITY HAS BEEN ATTACKED BY THE INVADERS!!!"
543 X1=INT(2*RND(71)+1):X2=X1+INT(2*RND(89)+1)
544 IF S*X2<I1 THEN PRINT ,"AND HAS FALLEN, SINCE YOU ONLY HAD";S;"SOLDIERS DEFENDING IT":GOTO 700
545 IF S*X1<I1 THEN 550
546 PRINT ,"BUT THE ATTACK HAS BEEN SUCCESSFULLY REPULSED BY YOUR VALIANT ARMY!"
547 I2=50+INT(20*RND(97)):GOTO 560
550 I1=I1-S*X1
551 PRINT ,"AND A PITCHED BATTLE IS TAKING PLACE OUTSIDE THE CITY"
560 SD=INT(I1/X2):S=S-SD:POP=POP-SD
570 Q=INT(12*RND(99)):IF Q>3 THEN 575
571 Q=INT(50*RND(101))+10
572 PRINT ,"A HORRIBLE PLAGUE HAS SWEPT THROUGH THE CITY; AND";Q;"% OF THE POPULATION HAS DIED!"
573 POP=POP-INT(POP*Q/100)
575 PRINT:ANS$="":LINE INPUT "              PRESS 'RETURN KEY' TO CONTINUE ...";ANS$
576 IF ANS$="Q" OR ANS$="QUIT" THEN 1100 ELSE 200
600 PRINT CHR$(126);CHR$(17);CHR$(0);CHR$(12)
601 IF ARMY>0 THEN PRINT:PRINT
602 PRINT CHR$(126);CHR$(24);
603 PRINT
604 PRINT ,"YOU STARVED";D;"PEOPLE THIS MONTH!"
650 PRINT
651 PRINT ,"THE PEOPLE HAVE GROWN WEARY OF YOUR EXTREME MIS-MANAGEMENT,"
652 PRINT ,"AND YOU HAVE BEEN REMOVED FROM OFFICE AND SENT INTO EXILE!!!"
653 GOTO 800
700 PRINT
710 INPUT "              DO YOU WISH TO TRY AGAIN";ANS$
720 IF ANS$="Y" OR ANS$="YES" THEN 100
730 IF ANS$="N" OR ANS$="NO" THEN 1100
740 GOTO 710
800 PRINT:PRINT
810 FOR K=1 TO 79
811 PRINT "-";
812 NEXT K
813 PRINT
815 PRINT ,"YOU MANAGED TO STAY IN OFFICE FOR";M;"MONTHS!!!"
820 PRINT
825 PRINT ,"DURING YOUR REIGN,";INT(P1);"PERCENT OF THE POPULATION"
830 PRINT ,"DIED OF STARVATION PER MONTH; A TOTAL OF";D1;"PEOPLE DIED."
840 IF POP<1 THEN PRINT ,"IN FACT, YOU KILLED EVERYONE AND GET THE ZPG AWARD!":G
OTO 700
850 LL=INT((A+1+(B-BX)/P)/POP)
860 PRINT
865 PRINT ,"YOU STARTED WITH 10 ACRES/PERSON AND ENDED WITH";LL;"ACRES/PERSON,"
866 PRINT ,"AFTER ADJUSTING THE CURRENT SUPPLY OF GRAIN AGAINST THE ORIGINAL."
870 PRINT
880 IF P1>3 THEN PRINT ,"YOU ARE AN EXTREMELY MALEVOLENT RULER!":GOTO 700
890 IF P1<1 THEN PRINT ,"YOUR BENEVOLENCE IS APPRECIATED BY YOUR LOVING SUBJECTS":GOTO 700
895 PRINT ,"YOUR ATTITUDE TOWARD THE POPULACE COULD BE IMPROVED":GOTO 700
900 ZZ=ZZ+1:IF ZZ<=3 THEN 1000
910 PRINT ,"HAMU, YOU SEEM TO BE HAVING A LITTLE DIFFICULTY"
920 PRINT ,"GRASPING THE COMPLEXITIES OF SUMERIAN GOVERNMENT ..."
930 PRINT ,"WOULD YOU LIKE TO REVIEW THE FACTS OF LIFE AGAIN";
940 ANS$="":INPUT ANS$:IF ANS$="Y" OR ANS$="YES" THEN GOSUB 100:GOSUB 201:ZZ=0:RETURN
1000 L=L+1:IF L<24 THEN RETURN
1010 PRINT CHR$(126);CHR$(17);CHR$(0);CHR$(12)
1020 IF ARMY=0 THEN L=12 ELSE L=14:PRINT:PRINT
1025 PRINT CHR$(126);CHR$(24);
1030 RETURN
1100 PRINT
1110 INPUT "              RETURN TO BASIC";ANS$
1120 IF ANS$<>"Y" AND ANS$<>"YES" THEN SYSTEM
1150 END