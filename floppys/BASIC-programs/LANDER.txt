10 DIM PLOT$(42)
15 WIDTH 79
20 PAG$=CHR$(126)+CHR$(28)+CHR$(126)+CHR$(18):CLR$=CHR$(126)+CHR$(24)
21 PRINT PAG$:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
22 PRINT ,"LL          A     NN   NN  DDDDDD   EEEEEEE  RRRRRR"
23 PRINT ,"LL        AA AA   NN   NN  DD   DD  EE       RR   RR"
24 PRINT ,"LL       AA   AA  NNN  NN  DD   DD  EE       RR   RR"
25 PRINT ,"LL       AAAAAAA  NN N NN  DD   DD  EEEEEEE  RRRRRR"
26 PRINT ,"LL       AA   AA  NN  NNN  DD   DD  EE       RRRR"
27 PRINT ,"LL       AA   AA  NN   NN  DD   DD  EE       RR  RR"
28 PRINT ,"LLLLLLL  AA   AA  NN   NN  DDDDDD   EEEEEEE  RR   RR"
29 PRINT:PRINT
30 INPUT "              DO YOU NEED INSTRUCTIONS";ANS$
40 IF ANS$="NO" OR ANS$="N" THEN GOSUB 2500:PRINT CLR$:GOTO 2050
50 IF ANS$<>"YES" AND ANS$<>"Y" THEN GOSUB 3000:GOTO 30
60 GOSUB 2500:GOSUB 80:GOTO 150
80 PRINT PAG$:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
90 PRINT ,"YOU ARE LANDING A SPACESHIP ON AN UNKNOWN PLANET AND HAVE"
110 PRINT ,"TAKEN OVER MANUAL CONTROL";H;"FT ABOVE A GOOD"
120 PRINT ,"LANDING SPOT WITH A DOWNWARD VELOCITY OF";INT(V);"FT/SEC,"
130 PRINT ,"AND";F;"UNITS OF ROCKET FUEL REMAINING!"
140 GOSUB 2400:RETURN
150 PRINT ,"THE FOLLOWING RULES GOVERN YOUR SPACECRAFT:"
160 PRINT:PRINT:PRINT ," (1) AFTER EACH SECOND, THE HEIGHT, VELOCITY, AND"
170 PRINT ,"     REMAINING FUEL WILL BE DISPLAYED, ALONG WITH A"
180 PRINT ,"     PLOT OF THE SPACECRAFT HEIGHT ABOVE THE"
190 PRINT ,"     PLANET'S ROCKY SURFACE"
200 PRINT:PRINT ," (2) AT THE RIGHT SIDE OF THE PLOT, THE SYMBOL '?'"
210 PRINT ,"     WILL BE DISPLAYED. AT THIS POINT, ENTER THE"
220 PRINT ,"     NUMBER OF UNITS OF FUEL TO BE BURNED IN"
230 PRINT ,"     THE NEXT SECOND. EACH UNIT OF FUEL SLOWS"
240 PRINT ,"     YOUR RATE OF DESCENT BY 1 FT/SEC."
250 PRINT:PRINT ," (3) THE MAXIMUM THRUST OF YOUR DESCENT ENGINE"
260 PRINT ,"     IS 50 UNITS OF FUEL/SEC OR 50 FT/SEC/SEC."
270 GOSUB 2400
280 PRINT ," (4) WHEN YOU MAKE CONTACT WITH THE PLANET'S SURFACE,"
290 PRINT ,"     YOUR DESCENT ENGINE WILL AUTOMATICALLY CUT"
300 PRINT ,"     OFF AND YOU WILL BE GIVEN A REPORT OF YOUR"
310 PRINT ,"     LANDING SPEED AND REMAINING FUEL."
320 PRINT:PRINT ," (5) IF YOU RUN OUT OF FUEL, THE '?' WILL NO LONGER"
330 PRINT ,"     BE DISPLAYED, AND THE REPORT WILL AUTOMATICALLY"
340 PRINT ,"     BE GIVEN FOR EACH SECOND UNTIL IMPACT."
341 PRINT:PRINT ," (6) THE MAXIMUM IMPACT VELOCITY THAT YOUR"
342 PRINT ,"     SPACECRAFT CAN WITHSTAND IS 7 FT/SEC."
343 PRINT:PRINT ," (7) DON'T FORGET TO ACCOUNT FOR THE ACCELERATION"
344 PRINT ,"     DUE TO THE PLANET'S GRAVITATIONAL FIELD."
350 GOSUB 2400:PRINT ,"LIST OF VARIABLES AND SYMBOLS USED:"
360 PRINT:PRINT:PRINT ," T = ELAPSED TIME (SEC)"
370 PRINT:PRINT ," H = HEIGHT ABOVE THE PLANET'S SURFACE (FT)"
380 PRINT:PRINT ," V = DOWNWARD VELOCITY (FT/SEC)"
390 PRINT:PRINT ," F = REMAINING UNITS OF FUEL"
400 PRINT:PRINT ," ? = USER PROMPT TO ENTER UNITS OF FUEL"
410 PRINT ,"     TO BE BURNED DURING THE NEXT SECOND"
420 PRINT:PRINT ," ";CHR$(64);" = REPRESENTS YOUR SPACECRAFT"
430 PRINT:PRINT ,"THE MANUEVERS WILL NOW BEGIN ..."
440 PRINT:PRINT ,"G O O D   L U C K   ! ! !"
450 GOSUB 2400
1100 PRINT PAG$
1150 PRINT PAG$:PRINT "TIME   HEIGHT   VEL.  FUEL";SPC(18);"PLOT OF HEIGHT"
1160 GRAV=G:TIME=0:SHIP$=CHR$(64):ALT=H:VEL=V:FUEL=F:SPLT=41
1170 FOR I=2 TO 40
1180 PLOT$(I)=" "
1190 NEXT I
1195 PLOT$(1)="*":PLOT$(41)=SHIP$:PLOT$(42)="*":A1=ALT
1200 B$=" ":BURN=0:PRINT SPC(1);
1205 PRINT USING "##";TIME;
1210 PRINT SPC(4);
1215 PRINT USING "###.#";ALT;
1220 PRINT SPC(3);
1225 PRINT USING "###.#";VEL;
1230 PRINT SPC(2);
1235 PRINT USING "####";FUEL;
1240 PRINT SPC(3);
1250 FOR I=1 TO 42
1260 PRINT PLOT$(I);
1270 NEXT I
1280 IF FUEL=0 THEN PRINT:BURN=0:GOTO 1400
1300 INPUT " ";B$
1302 IF B$="R" THEN G=GX:H=HX:V=VX:F=FX:GOTO 1100
1304 IF B$="Q" THEN 4000
1308 BURN=VAL(B$)
1310 BURN=INT(BURN)
1320 IF BURN<0 OR BURN>50 THEN GOSUB 2100:GOTO 1300
1330 IF BURN>FUEL THEN BURN=FUEL
1400 ACC=GRAV-BURN:V1=ACC+VEL:A2=ALT-VEL-.5*ACC:A3=INT(A2)
1410 IF A3>0 THEN 1500
1420 IF A3=0 THEN TX=ALT/VEL:GOTO 1450
1430 X=2*ACC*ALT+VEL*VEL:GOSUB 2200
1435 IF ACC=0 THEN TX=0 ELSE TX=(SQ-VEL)/ACC
1436 V1=ACC*TX+VEL
1437 IF V1<0 THEN 1500
1450 IX=1:TIME=TIME+TX:GOTO 1600
1500 IX=0:ALT=A2:TIME=TIME+1
1600 FUEL=FUEL-BURN:VEL=V1
1610 IF IX=1 THEN 1900
1620 SPLT=INT(ALT*39/A1+2.5)
1630 IF SPLT<=1 THEN SPLT=2
1640 IF SPLT<=41 THEN SHIP$=CHR$(64) ELSE SPLT=41:SHIP$=" "
1650 FOR I=2 TO 41
1660 PLOT$(I)=" "
1670 NEXT I
1680 PLOT$(SPLT)=SHIP$:GOTO 1200
1900 V1=INT(VEL):PRINT:PRINT
1905 IF V1=0 THEN PRINT "PERFECT LANDING!!!":GOTO 1990
1910 IF V1<=2 THEN PRINT "SLIGHTLY BUMPY LANDING.":GOTO 1990
1915 IF V1<=5 THEN PRINT "VERY BUMPY LANDING.":GOTO 1990
1920 IF V1<=7 THEN PRINT "CRUNCH!!! - YOU BARELY MADE IT!":GOTO 1990
1925 IF V1<=25 THEN PRINT "APPROPRIATE CONDOLENCES WILL BE SENT TO YOUR FAMILY.":GOTO 1990
1930 IF V1<=50 THEN 1940 ELSE 1960
1940 PRINT "EXCESSIVE IMPACT VELOCITY HAS DESTROYED THE"
1945 PRINT "STRUCTURAL INTEGRITY OF YOUR SHIP - NO AIR LEFT!!!":GOTO 1990
1960 PRINT "THAT FLASH OF LIGHT VISIBLE FROM THE EARTH"
1965 PRINT "WAS YOUR SPACECRAFT VAPORIZING UPON IMPACT ..."
1990 PRINT:PRINT:PRINT "FINAL VELOCITY:";
1991 PRINT USING "######.#";VEL;:PRINT " FT/SEC."
1992 PRINT "FUEL REMAINING:     ";
1993 PRINT USING "###";FUEL;:PRINT " UNITS."
1994 PRINT:PRINT
1995 IF V1<=2 THEN  PRINT "CONGRATULATIONS ... THE ADMIRAL SENDS HIS COMPLIMENTS!":GOTO 1999
1996 IF V1<=7 THEN PRINT "A REFRESHER PILOTS' TRAINING COURSE IS INDICATED.":GOTO 1999
1997 PRINT "YOU HAVE BEEN COURT MARTIALED."
1999 PRINT:PRINT
2000 INPUT "DO YOU WANT TO TRY AGAIN";ANS$
2010 IF ANS$="NO" OR ANS$="N" THEN PRINT PAG$:GOTO 9999
2020 IF ANS$<>"YES" AND ANS$<>"Y" THEN GOSUB 3000:GOTO 2000
2025 PRINT PAG$:PRINT:PRINT:PRINT:PRINT:PRINT
2030 INPUT "SAME INITIAL CONDITIONS";ANS$
2040 IF ANS$="YES" OR ANS$="Y" THEN G=GX:H=HX:V=VX:F=FX:GOSUB 80:GOTO 1150
2045 IF ANS$<>"NO" AND ANS$<>"N" THEN GOSUB 3000:GOTO 2030
2050 PRINT:INPUT "              DO YOU WISH TO CHOOSE INITIAL CONDITIONS";ANS$
2060 IF ANS$="NO" OR ANS$="N" THEN GOSUB 2500:GOSUB 80:GOTO 1100
2070 IF ANS$<>"YES" AND ANS$<>"Y" THEN GOSUB 3000:GOTO 2050
2080 GOSUB 2600:GOTO 1150
2100 FOR I=1 TO 10
2110 PRINT CHR$(8);
2120 NEXT I
2130 PRINT SPC(9)
2140 FOR I=1 TO 9
2150 PRINT CHR$(8);
2160 NEXT I
2170 RETURN
2200 IF X=0 THEN SQ=0:RETURN
2210 IF X<1 THEN X1=2*X ELSE X1=X/2
2300 X2=.5*(X/X1+X1):D=X2-X1
2310 IF D<0 THEN D=-D
2320 IF D>1E-04 THEN X1=X2:GOTO 2300
2330 SQ=X2:RETURN
2400 PRINT:PRINT
2410 LINE INPUT "              PRESS 'RETURN' KEY TO CONTINUE ";ANS$
2415 IF ANS$="Q" OR ANS$="QUIT" THEN 4000
2416 IF ANS$="R" OR ANS$="RESTART" THEN 21
2420 PRINT PAG$:RETURN
2500 G=INT(100*RND(47))
2501 IF G<5 THEN G=G+5:GOTO 2503
2502 IF G>25 THEN G=G-17:GOTO 2502
2503 V=INT(100*RND(G))
2504 IF V<30 THEN V=V+7:GOTO 2504
2505 IF V>50 THEN V=V-11:GOTO 2505
2506 H=INT(1000*RND(V))
2507 IF H<400 THEN H=H+39:GOTO 2507
2508 IF H>499 THEN H=H-47:GOTO 2508
2509 GOSUB 2900:RETURN
2600 INPUT "GRAVITATIONAL ACCELERATION CONSTANT (5-35)";G
2610 IF G<5 OR G>35 THEN 2600
2700 INPUT "INITIAL VELOCITY (30-60)";V
2710 IF V<30 OR V>60 THEN 2700
2720 H1=15*INT(V):H2=20*INT(V)
2800 PRINT "INITIAL ALTITUDE (";INT(H1);"-";INT(H2);")";
2810 INPUT H
2820 IF H<H1 OR H>H2 THEN 2800
2830 GOSUB 2900:RETURN
2900 F=INT(1000*RND(H))
2901 IF F<100 THEN F=F+23:GOTO 2901
2902 IF F>139 THEN F=F-29:GOTO 2902
2903 F=F+INT(2.5*((2*H*G/V+V)/10))
2904 IF H>449 THEN F=F+INT(F/10)
2910 GX=G:HX=H:VX=V:FX=F:RETURN
3000 PRINT:PRINT ,"I CANNOT UNDERSTAND YOUR LAST ENTRY ..."
3010 PRINT ,"PLEASE, ANSWER MY QUESTIONS WITH EITHER"
3020 PRINT ,"A 'Y' FOR 'YES' OR 'N' FOR 'NO' ! ! !"
3030 BCK$=CHR$(126)+CHR$(12)
3040 PRINT BCK$;BCK$;BCK$;BCK$;BCK$;
3045 PRINT CHR$(126);CHR$(15);
3050 RETURN
4000 INPUT "RETURN TO BASIC";ANS$
4010 IF ANS$="N" OR ANS$="NO" THEN SYSTEM
9999 END